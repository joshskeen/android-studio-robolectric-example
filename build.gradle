// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'

        // plugin for reporting code coverage to coveralls
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// set flag whether we are on a build server
ext.isCi = ("true".equals(System.getenv("TRAVIS")) || "true".equals(System.getenv("CIRCLECI")))

// current robolectric has no support for android M (v23) https://github.com/robolectric/robolectric/issues/1865
ext.projectAndroidVersion = 22

// latest supported version by google playstore app is v8 (august 2015)
// latest supported version by joda time library is v10
ext.projectAndroidMinVersion = 10

// sometimes support tools and build tools have same version but support tools version grow faster with time
// current robolectric has no support for android M (v23) stuff
ext.projectAndroidBuildToolsVersion = "22.0.1"
ext.projectAndroidSupportToolsVersion = "22.2.1"

// not all jacoco versions work without issues
// TODO try which is the latest working version without issues, with 0.7.5 is saw some
ext.projectJacocoVersion = "0.7.0.201403182114"

allprojects {
    repositories {
        jcenter()
    }
}

// ensure clean is also triggered for root build folder
apply plugin: 'java'

// support to see all reports on one page
apply plugin: 'build-dashboard'
buildDashboard {
    reports.html.destination = "build/"
}
buildDashboard << {
    println file("build/index.html").absolutePath
}

// just clean up dashboard from not generated reports
test.reports.html.enabled = false
test.reports.junitXml.enabled = false

// support combined test code coverage for all levels (unit, component, integration)
apply from: "jacoco.gradle"